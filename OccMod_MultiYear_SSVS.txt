model {
  #-------------------------------------------------------------------------------
  #######################################
  ############ ASSIGN PRIORS ############
  #######################################
  #-------------------------------------------------------------------------------
  ## Metacommunity-wide average of parameters: psi, p, phi, gamma:
  
  psiMean ~ dbeta(1,1) # Initial occurrence probability
  pMean ~ dbeta(1,1) # Detection probability
  phiMean ~ dbeta(1,1) # Persistence probability
  gamMean ~ dbeta(1,1) # Colonization probability
  
  ## Manual logit transformation of mean parameter values:
  
  lpsiMean <- log(psiMean) - log(1-psiMean)
  lpMean <- log(pMean) - log(1-pMean) 
  lphiMean <- log(phiMean) - log(1-phiMean) 
  lgamMean <- log(gamMean) - log(1-gamMean) 
  
  #-------------------------------------------------------------------------------
  ## Precision estimates for each metacommunity average:
  
  lpsiSD ~ dunif(0,10)
  lpSD ~ dunif(0,10)
  lphiSD ~ dunif(0,10)
  lgamSD ~ dunif(0,10)
  
  lpsiPrec <- pow(lpsiSD,-2)
  lpPrec <- pow(lpSD,-2)
  lphiPrec <- pow(lphiSD,-2)
  lgamPrec <- pow(lgamSD,-2)
  
  #-------------------------------------------------------------------------------
  ## Estimates for covariate effects:
  ## Stochastic search variable selection priors:
  
  for(i in 1:n){
    bSD[i] ~ dunif(0,10)
    bPrec_in[i] <- pow(bSD[i],-2)
    bPrec[i,1] <- bPrec_in[i]         #coeff. effectively zero
    bPrec[i,2] <- bPrec_in[i]/500    #non-zero coeff.
  
    for(t in 1:(T-1)){
      cSD[i,t] ~ dunif(0,10)  
      cPrec_in[i,t] <- pow(cSD[i,t],-2)
      cPrec[i,1,t] <- cPrec_in[i,t]         #coeff. effectively zero
      cPrec[i,2,t] <- cPrec_in[i,t]/500    #non-zero coeff.
  
      dSD[i,t] ~ dunif(0,10)
      dPrec_in[i,t] <- pow(dSD[i,t],-2)
      dPrec[i,1,t] <- dPrec_in[i,t]         #coeff. effectively zero
      dPrec[i,2,t] <- dPrec_in[i,t]/500
    }
  }

  p_ind[1] <- 1/2
  p_ind[2] <- 1 - p_ind[1]
  
  ## Missing data priors:
  #-------------------------------------------------------------------------------
  for(t in 1:T){
    for(cov in 1:10){
      sd.x[cov,t] ~ dunif(0, 10)
      Prec.x[cov,t] <- pow(sd.x[cov,t],-2)
    }
  }
  for(t in 1:T){
    for(cov in 11:21){
      bern.p[cov,t] ~ dbeta(1,1)
    }
  }
  
  p.hydro[1] <- 1/3; p.hydro[2] <- 1/3; p.hydro[3] <- 1/3; 
  #-------------------------------------------------------------------------------
  #######################################
  ########## Likelihood Model ###########
  #######################################
  #-------------------------------------------------------------------------------
  
  # Deal with the missing covariates:
  for(t in 1:T){
    for(cov in 1:10){
      for(k in 1:K){
        x[k, cov, t] ~ dnorm(0, Prec.x[cov,t])
      }
    }
  }
  for(t in 1:T){
    for(cov in 11:21){
      for(k in 1:K){
        x[k, cov, t] ~ dbern(bern.p[cov,t])
      }
    }
  }
  for(t in 1:T){
    for(k in 1:K){
      x[k, 22, t] ~ dcat(p.hydro[])
    }
  }
  
  # Occupancy states:
  for (i in 1:n) { # n=number of species
    
    ## Initial occupancy state at t=1
    
    b0[i] ~ dnorm(lpsiMean, lpsiPrec)T(-12,12) # Species-specific baseline of occurrence prob.
    
    ## Species-specific covariate effects on initial occurrence:
    for (cov in 1:ncovs) {
      b_indA[i, cov] ~ dcat(p_ind[]) # returns 1 or 2
      b_incl[i, cov] <- b_indA[i, cov] - 1   # returns 0 or 1
      b[i,cov] ~ dnorm(0, bPrec[i, b_indA[i, cov]])T(-12,12)

      for(k in 1:K){
          temp.b[i, k, cov, 1] <- b[i, cov] * x[k, cov, 1]
        }
      }
    
    lp[i,1] ~ dnorm(lpMean, lpPrec)T(-12,12) # Species-specific detection prob at t=1
    p[i,1] <- 1/(1+exp(-lp[i,1])) # Manual anti-logit
    
    for (k in 1:K) {
      lpsi[i,k,1] <- b0[i] + sum(temp.b[i, k, ,1]) # Year-specific covariate values
      psi[i,k,1] <- 1/(1 + exp(-lpsi[i,k,1]))
      mu.z[i,k,1] <- psi[i,k,1]
      z[i,k,1] ~ dbern(mu.z[i,k,1])
      mu.y[i,k,1] <- p[i,1]*z[i,k,1]
      y[i,k,1] ~ dbin(mu.y[i,k,1], J[k,1]) # J=number of hosts sampled from each site
    }
    
    ## Model of changes in occupancy state for t=2, ..., T
    
    # First, draw covariate effects:
    # - These are species-specific
    # - I assume covariate effects don't change across years
    
    for (cov in 1:ncovs) {
      for(t in 1:(T-1)){
        c_indA[i,cov,t] ~ dcat(p_ind[]) # returns 1 or 2
        c_incl[i,cov,t] <- c_indA[i,cov,t] - 1   # returns 0 or 1
        c[i,cov,t] ~ dnorm(0, cPrec[i, c_indA[i, cov], t])T(-12,12)
      
        d_indA[i,cov,t] ~ dcat(p_ind[]) # returns 1 or 2
        d_incl[i,cov,t] <- d_indA[i,cov,t] - 1   # returns 0 or 1
        d[i,cov,t] ~ dnorm(0, dPrec[i, d_indA[i,cov,t], t])T(-12,12)
      
        for(k in 1:K){
          temp.c[i, t, k, cov] <- c[i,cov,t] * x[k,cov,t]
          temp.d[i, t, k, cov] <- d[i,cov,t] * x[k,cov,t]
        }
      }
    }
    
    for (t in 1:(T-1)) {
      lp[i,t+1] ~ dnorm(lpMean, lpPrec)T(-12,12) 
      p[i,t+1] <- 1/(1+exp(-lp[i,t+1])) # Species- and year-specific detection prob.
      
      c0[i,t] ~ dnorm(lgamMean, lgamPrec)T(-12,12) # Species- and year-specific baseline 
      # colonization probability 
      d0[i,t] ~ dnorm(lphiMean, lphiPrec)T(-12,12) # Species- and year-specific baseline 
      # persistence probability
      
      for (k in 1:K) { # K=number of sites
        lgam[i,k,t] <- c0[i,t] + sum(temp.c[i, t, k, ])
        gam[i,k,t] <-  1/(1+exp(-lgam[i,k,t]))
        lphi[i,k,t] <- d0[i,t] + sum(temp.d[i, t, k, ])
        phi[i,k,t] <-  1/(1+exp(-lphi[i,k,t]))
        
        psi[i,k,t+1] <- phi[i,k,t] * psi[i,k,t] + gam[i,k,t] * (1 - psi[i,k,t])
        mu.z[i,k,t+1] <- (phi[i,k,t] * z[i,k,t] + gam[i,k,t] * (1-z[i,k,t]))
        z[i,k,t+1] ~ dbern(mu.z[i,k,t+1])
        mu.y[i,k,t+1] <- p[i,t+1] * z[i,k,t+1]
        y[i,k,t+1] ~ dbin(mu.y[i,k,t+1], J[k,t+1])
      } 
    }
  } 
  
}